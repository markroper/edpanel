allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        mavenCentral()
    }
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
    }
}

subprojects {
    evaluationDependsOn(':common')

    compileJava {
        options.compilerArgs << "-Werror"
        options.compilerArgs << "-Xlint:unchecked"
    }

    test {
        useTestNG() {
            useDefaultListeners = true
            def suiteFile = "src/test/resources/${project.name}-unit.xml"
            options.suites(suiteFile)
        }

        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }
    }
}

task integrationCodeCoverageReport(type: JacocoReport) {
    group 'Verification'
    description 'runs the integration tests against a local container with code coverage'
    
    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled true
        html.destination "${buildDir}/reports/jacoco"
        csv.enabled false
    }
}

def loadProperty(pathRelToProjectDir, property) {
    Properties props = new Properties()
    props.load(new FileInputStream(projectDir.toString() + pathRelToProjectDir))
    return props.get(property).toString()
}

task resetDatabase(type: Exec) {
    group 'Database'
    description 'drops the database and re-adds an empty one'

    def database_user = loadProperty("/warehouse/src/main/resources/database.properties", "database.user");
    def database_pass = loadProperty("/warehouse/src/main/resources/database.properties", "database.pass");
    def database_host = loadProperty("/warehouse/src/main/resources/database.properties", "database.host");
    def database_port = loadProperty("/warehouse/src/main/resources/database.properties", "database.port");
    def database_name = loadProperty("/warehouse/src/main/resources/database.properties", "database.name");

    commandLine 'mysql', '-u' + database_user, '-p' + database_pass, '--port=' + database_port, '--host=' + database_host
    standardInput file("${project.rootDir.absolutePath}/database/cleanMySqlDatabase.sql").newInputStream()
}

task refillDatabase(type: Exec, dependsOn: resetDatabase) {
    group 'Database'
    description 'creates everything needed within the database'

    def database_user = loadProperty("/warehouse/src/main/resources/database.properties", "database.user");
    def database_pass = loadProperty("/warehouse/src/main/resources/database.properties", "database.pass");
    def database_host = loadProperty("/warehouse/src/main/resources/database.properties", "database.host");
    def database_port = loadProperty("/warehouse/src/main/resources/database.properties", "database.port");
    def database_name = loadProperty("/warehouse/src/main/resources/database.properties", "database.name");

    commandLine 'mysql', '-u' + database_user, '-p' + database_pass, '--port=' + database_port, '--host=' + database_host
    standardInput file("${project.rootDir.absolutePath}/database/createTableDDL.sql").newInputStream()
}

// run the integration tests before generating the report
integrationCodeCoverageReport.dependsOn {
      project(':warehouse').integrationLocalWithContainer
}

